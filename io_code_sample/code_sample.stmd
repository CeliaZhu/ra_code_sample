---
title: Stata Code Sample [^1]
author: Xiling (Celia) Zhu <xiling@uchicago.edu>
date: Dec 23, 2020
header-includes:
  - \usepackage{multicol, graphicx, float, booktabs}
---

## 0. Overview

This task was inspired by IO research on the automobile industry. I was
provided with car model sales data in five European markets. `car data` contains
the manufacturing characteristics and the quantity sold of each car model within
each market, from 1970 to 1990. `market data` contains the GDP, population, and
tax rate of each market in each year.

[^1]: Generated by `markstat`. For source code, please see ---------------------

## 1. Data Cleaning

The central task of the section is to merge `car data` and `market data` into
one panel with 3 dimensions: car model ($i$), market ($j$), and year ($t$). In
each row, it contains car model manufacturing characteristics, price, and quantity
sold, market GDP, population, and tax rate.

Set up directories to store the data, figures, tables, and log, respectively.

```{s}
clear all
cap log close
set more off
set varabbrev off

* Data directory
global data "/Users/celiazhu/Box/projects/ra_code_sample/io_code_sample/data"

* Figure directory
global figure "/Users/celiazhu/Box/projects/ra_code_sample/io_code_sample/output/figure"

* Table directory
global table "/Users/celiazhu/Box/projects/ra_code_sample/io_code_sample/output/table"

* Log directory
global log "/Users/celiazhu/Box/projects/ra_code_sample/io_code_sample/output/log"
log using "$log/stata_code_sample.log", replace
```

Append all car data.

```{s}
local carcsv: dir "$data/car_data" files "*.csv"
foreach file of local carcsv {
  preserve
  insheet using "$data/car_data/`file'", clear
  cap destring sp, force replace // change sp (maximum speed) from string to numeric
  replace ye = 1900 + ye // use full year
  foreach var of varlist ma loc {
    replace `var' = "United Kingdom" if `var' == "UK" // use full country name
  }
  save temp, replace
  restore
  append using temp
}

save "$data/clean_data/car_data_all.dta", replace
rm temp.dta
```

Append all market data.

```{s}
clear
local mktcsv: dir "$data/market_data" files "*.csv"
foreach file of local mktcsv {
  preserve
  insheet using "$data/market_data/`file'", clear
  save temp, replace
  restore
  append using temp
}
```

Spell out full country names in appended market data.

```{s}
local country_list `" "Belgium" "France" "Germany" "Italy" "United Kingdom" "'
foreach country in `country_list' {
replace ma = "`country'" if ma == substr("`country'", 1, 1)
}
save "$data/clean_data/market_data_all.dta", replace
rm temp.dta
```

Merge car and market data.

```{s}
merge 1:m ye ma using "$data/clean_data/car_data_all.dta", nogen
```

Fix missing values of fuel consumption variables.

```{s}
destring li*, force replace
replace li = (li1 + li2 + li3)/3 if li == .
foreach var of varlist li1 li2 li3 {
  replace `var' = 0 if `var' == .
  replace `var' = li*3 - (li1 + li2 + li3) if `var' == 0
}
```

Fix data type.

```{s}
destring ac, force replace // ac (time to acceleration) is float
foreach var of varlist ma loc brand model cla frm {
  encode `var', gen(`var'_code) // transform string to categorical variables
}
```

Label needed variables.

```{s}
label var hp "Horsepower (kW)"
label var li "Fuel consumption (liter per km)"
label var eurpr "Price in common currency"
```

Store cleaned data.

```{s}
save "$data/clean_data/all_data.dta", replace
```

## 2. Data Exploration

This section uses the model-market-year panel dataset to visualize the relationship
between fuel consumption (`li`) and horsepower (`hp`) in the years 1970 and 1990.

```{s}
preserve
tempfile data_70
keep if ye == 1970
keep ye qu hp li
save `data_70', replace
restore

preserve
tempfile data_90
keep if ye == 1990
keep ye qu hp li
save `data_90', replace
restore
```

For the years 1970 and 1990, group cars by decile of observed horsepower in that
year, and then compute the sales-weighted average of fuel consumption for cars in
each horsepower decile.

For each year, produce a scatter plot of the sales-weighted average of fuel
consumption versus the midpoint of each horsepower decile.

For each year, regress fuel consumption on a constant, horsepower, and
log(horsepower), using sales as sample weights. Display the fitted curves on the
scatterplot.

```{s}
foreach year of numlist 70 90 {
  use `data_`year'', clear
  xtile hp_decile = hp, nq(10)
  bysort hp_decile: asgen wtd_avg_li = li, weight(qu)
  gen loghp = ln(hp)
  * fit curves; use sales as sample weights
  reg li hp loghp [pw = qu]
  predict li_hat
  * range and midpoint of each decile group
  gen hp_mid = .
  gen hp_min = .
  gen hp_max = .
  * total sales of each decile group
  gen qu_total = .
  * loop over each horsepower decile group
  forvalues  i = 1/10 {
      * horsepower summary statistics
      su hp if hp_decile == `i'
      local hp_min r(min)
      local hp_max r(max)
      replace hp_min = `hp_min' if hp_decile == `i'
      replace hp_max = `hp_max' if hp_decile == `i'
      replace hp_mid = (hp_min + hp_max)/2 if hp_decile == `i'
      * sales summary statistics
      su qu if hp_decile == `i'
      local qu_total r(sum)
      replace qu_total = `qu_total' if hp_decile == `i'
  }
  save `data_`year'', replace
}

* Aggregated data of fuel consumption and horsepower in 1970 and 1990
use `data_70', clear
append using `data_90'
collapse (first) wtd_avg_li hp_min hp_max hp_mid li_hat qu_total, by(ye hp_decile)
label var wtd_avg_li "Sales-weighted average of fuel consumption"
label var hp_mid "Midpoint of horsepower decile"
```

Visualize the relationship between fuel consumption and horsepower.

```{s}
twoway (scatter wtd_avg_li hp_mid if ye == 1970, mcolor(navy%70)) ///
(scatter wtd_avg_li hp_mid if ye == 1990, mcolor(orange%70)) ///
(line li_hat hp_mid if ye == 1970, lcolor(navy%70) lpattern(shortdash)) ///
(line li_hat hp_mid if ye == 1990, lcolor(orange%70) lpattern(longdash)), ///
xlabel(15(15)150) ylabel(5(2)15) ///
xtitle("Midpoint of each horsepower decile") ///
ytitle("Sales-weighted average of fuel consumption") ///
title("Relationship between Fuel Consumption and Horsepower in 1970 and 1990", ///
size(medsmall) color(black)) ///
legend(label(1 "1970") label(2 "1990") ///
label(3 "1970 fitted curve") label(4 "1990 fitted curve") ///
nobox region(lcolor(white)) size(small) rows(1)) ///
graphregion(color(white))
graph export "$figure/relation_li_hp.png", replace
```
\begin{figure}[H]
\centering
\caption{Relationship between fuel consumption and horsepower by year}
\includegraphics[width = \textwidth]{/Users/celiazhu/Box/projects/ra_code_sample/io_code_sample/output/figure/relation_li_hp.png}
\end{figure}

Create publication quality table of the sales-weighted average of fuel consumption
by decile of horsepower in 1970 and 1990.

```{s}
* prepare data to generate texsave table
reshape wide wtd_avg_li hp_min hp_max hp_mid li_hat qu_total, i(hp_decile) j(ye)
foreach year of numlist 1970 1990 {
  * horspower range of each decile group
  egen hp_range`year' = concat(hp_min`year' hp_max`year'), punct("--")
  order hp_range`year', before(qu_total`year')
}

* drop un-needed variables
drop hp_min* hp_max* hp_mid* li_hat*

* generate summary statistics table
local file_name hp_sum_table
local title title("Sales-Weighted Average of Fuel Consumption by Decile of Horsepower")
local midliners "\cmidrule(lr){2-4} \cmidrule(lr){5-7} \addlinespace[-2.5ex]"
local colnames "{Decile Group} &{Fuel consumption} &{Range} &{Sales} &{Fuel consumption} &{Range} &{Sales}"
local headerlines headerlines("& \multicolumn{3}{c}{1970} & \multicolumn{3}{c}{1990}" "`midliners'" "`colnames'")
local fn footnote("Fuel consumption represents the sales-weighted average of fuel consumption")
local marker marker("tbl1")
local size size("small")
texsave using "$table/`file_name'.tex", replace frag nonames `headerlines' `marker' `fn' `size' `title'
```

\begin{table}[H]
\centering
\caption{Structural parameters for fuel consumption and price}
\input{"/Users/celiazhu/Box/projects/ra_code_sample/io_code_sample/output/table/hp_sum_table.tex"}
\end{table}


## 3. Estimation and Causal Inference

For each car model $i$, market $j$, and year $t$, construct the outcome variable
$Y_{ijt} = log(S_{ijt}) - log(S_{0ijt})$, where $N_{jt}$ is the number of
consumers in market $j$ year $t$, assuming the average size of family is four and
each family potentially buys one car or no cars in a given year, $S_{ijt}$ is the
market share for car model $i$ in market $j$ year $t$, $S_{0jt}$ is the share of
consumers who buy no cars in market $j$ year $t$.

A Standard logit demand model:
$$
U_{cijt} = \beta_c \text{FuelConsumption}_{ijt} + \alpha_c Price_{ijt} + \epsilon_{cijt}
$$

where $c$ is an index for car consumers, $i$ car model, $j$ market, and $t$ year.
$U$ is the indirect utility, as a function of the car models' average fuel consumption
and price.

Assume the structural parameter, $\beta$ and $\alpha$, does not depend on individual
consumer (every consumer has the same taste for fuel consumption and price),
$\epsilon_{cijt}$ has type-one extreme value function, and all consumers
who did not buy a car chose the outside option, which gives zero utility. Then,
we can estimate the structural parameter through the following model:

$$
log(S_{ijt}) - log(S_{0ijt}) = \beta \text{FuelConsumption}_{ijt} + \alpha Price_{ijt} + \eta_{ijt}
$$

```{s}
use "$data/clean_data/all_data.dta", clear

* Generate the number of consumers in market j in year t
gen n_consumers = pop/4
label var n_consumers "Number of consumers"

* Generate market share of car model i in market j in year t
gen share = qu/n_consumers
label var share "Market share"

* Generate share of consumers who by no cars in market j in year t
bysort ma_code ye: egen qu_total = total(qu)
gen share_no_cars = 1 - qu_total/n_consumers
label var share_no_cars "Share of consumers who buy no cars"

* Generate outcome variable
gen outcome = ln(share) - ln(share_no_cars)
label var outcome "Outcome"

* Regress outcome variable on a constant, fuel consumption, and price
eststo clear
eststo: qui reg outcome li eurpr, r
estadd local fe "None"
```

$beta$ is the ``taste" parameter consumers have for fuel consumption.

Price is an endogenous variable in our model.

We can mitigate this problem by including car model, market, and year fixed effects.

```{s}
* Including car model, market, and year fixed effects
eststo: qui reg outcome li eurpr i.ma_code i.model_code i.ye, r
estadd local fe "car model, market, and year"

esttab using "$table/linear.tex", replace se drop(*.ma_code *.model_code *.ye) ///
booktabs width(\textwidth) label scalars("fe Fixed effects")
```

\begin{table}[H]
\centering
\caption{Structural parameters for fuel consumption and price}
\input{"/Users/celiazhu/Box/projects/ra_code_sample/io_code_sample/output/table/linear.tex"}
\end{table}
