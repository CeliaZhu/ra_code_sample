---
title: Stata Code Sample [^1]
author: Xiling (Celia) Zhu <xiling@uchicago.edu>
date: Dec 23, 2020
header-includes:
  - \usepackage{multicol, graphicx, float, booktabs, tabularx, hyperref, amsmath}
  - \hypersetup{
    colorlinks=true,
    linkcolor=blue,
    filecolor=magenta,
    urlcolor=blue
    }
---

## 0. Overview

This task was inspired by IO research on the automobile industry. I was
provided with car model sales data in five European markets. `car data` contains
the manufacturing characteristics and the quantity sold of each car model within
each market, from 1970 to 1990. `market data` contains the GDP, population, and
tax rate of each market in each year.

[^1]: Generated by `markstat`. For source code, please see the link [here](https://github.com/CeliaZhu/ra_code_sample/blob/master/io_code_sample/code_sample.stmd).

## 1. Data Cleaning

The central task of the section is to merge `car data` and `market data` into
one panel with 3 dimensions: car model ($i$), market ($j$), and year ($t$). In
each row, it contains car model manufacturing characteristics, price, and quantity
sold, market GDP, population, and tax rate.

Set up directories to store the data, figures, tables, and log, respectively.

```{s}
clear all
set more off
set varabbrev off

* Data directory
global data "/Users/celiazhu/Box/projects/ra_code_sample/io_code_sample/data"

* Figure directory
global figure "/Users/celiazhu/Box/projects/ra_code_sample/io_code_sample/output/figure"

* Table directory
global table "/Users/celiazhu/Box/projects/ra_code_sample/io_code_sample/output/table"
```

Append all car data.

```{s}
local carcsv: dir "$data/car_data" files "*.csv"
foreach file of local carcsv {
  preserve
  qui insheet using "$data/car_data/`file'", clear
  qui cap destring sp, force replace // change sp (maximum speed) from string to numeric
  qui replace ye = 1900 + ye // use full year
  foreach var of varlist ma loc {
    qui replace `var' = "United Kingdom" if `var' == "UK" // use full country name
  }
  qui save temp, replace
  restore
  qui append using temp
}
save "$data/clean_data/car_data_all.dta", replace
rm temp.dta
```

Append all market data.

```{s}
clear
local mktcsv: dir "$data/market_data" files "*.csv"
foreach file of local mktcsv {
  preserve
  qui insheet using "$data/market_data/`file'", clear
  qui save temp, replace
  restore
  qui append using temp
}
```

Spell out full country names in appended market data.

```{s}
local country_list `" "Belgium" "France" "Germany" "Italy" "United Kingdom" "'
/* Abbreviated market name corresponds to the first letter of the full country
name, and the correspondence is unique in this dataset. */
foreach country in `country_list' {
qui replace ma = "`country'" if ma == substr("`country'", 1, 1)
}
save "$data/clean_data/market_data_all.dta", replace
rm temp.dta
```

Merge car and market data.

```{s}
merge 1:m ye ma using "$data/clean_data/car_data_all.dta", nogen
```

Fix missing values of fuel consumption variables.

```{s}
qui destring li*, force replace
replace li = (li1 + li2 + li3)/3 if li == .
foreach var of varlist li1 li2 li3 {
  qui replace `var' = 0 if `var' == .
  qui replace `var' = li*3 - (li1 + li2 + li3) if `var' == 0
}
```

Fix data type.

```{s}
qui destring ac, force replace // ac (time to acceleration) is float
foreach var of varlist ma loc brand model cla frm {
  qui encode `var', gen(`var'_code) // transform string to categorical variables
}
```

Label needed variables.

```{s}
label var hp "Horsepower (kW)"
label var li "Fuel consumption (liter per km)"
label var eurpr "Price in common currency"
```

Store cleaned data.

```{s}
qui save "$data/clean_data/all_data.dta", replace
```

## 2. Data Exploration

This section uses the model-market-year panel dataset to visualize the relationship
between fuel consumption (`li`) and horsepower (`hp`) in the years 1970 and 1990.

```{s}
preserve
// a temporary dataset for 1970 data
tempfile data_70
* keep 1970 data
keep if ye == 1970
* keep neede variables
keep ye qu hp li ma_code model_code
qui save `data_70', replace
restore

preserve
// a temporary dataset for 1970 data
tempfile data_90
* keep 1990 data
keep if ye == 1990
* keep needeed variables
keep ye qu hp li ma_code model_code
qui save `data_90', replace
restore
```

For the years 1970 and 1990, group cars by decile of observed horsepower in that
year, and then compute the sales-weighted average of fuel consumption for cars in
each horsepower decile.

For each year, produce a scatter plot of the sales-weighted average of fuel
consumption versus the midpoint of each horsepower decile.

For each year, regress fuel consumption on a constant, horsepower, and
log(horsepower), using sales as sample weights. Display the fitted curves on the
scatterplot.

```{s}
/* 1) group cars by decile of horsepower; 2) fit curves; 3) summary statistics
of horsepower and sales in each decile group;

compute midpoint of each decile group;
 compute sales-weighted average of fuel consumption; 4) fit curves*/
foreach year of numlist 70 90 {
  use `data_`year'', clear
  // 1) group cars by decile of horsepower
  xtile hp_decile = hp, nq(10)
  bysort hp_decile: asgen wtd_avg_li = li, weight(qu)
  // 2) fit curves; use sales as sample weights
  qui gen loghp = ln(hp)
  sort ye ma_code model_code
  qui reg li hp loghp [pw = qu]
  qui predict li_hat
  // 3) summary statistics of horsepower and sales in each decile group
  qui gen hp_mid = .
  qui gen hp_min = .
  qui gen hp_max = .
  qui gen qu_total = .
  * loop over each horsepower decile group
  forvalues  i = 1/10 {
    * horsepower summary statistics
    qui su hp if hp_decile == `i'
    local hp_min r(min)
    local hp_max r(max)
    qui replace hp_min = `hp_min' if hp_decile == `i'
    qui replace hp_max = `hp_max' if hp_decile == `i'
    * midpoint of each decile group for x-axis of scatterplot
    qui replace hp_mid = (hp_min + hp_max)/2 if hp_decile == `i'
    * sales summary statistics
    qui su qu if hp_decile == `i'
    local qu_total r(sum)
    qui replace qu_total = `qu_total' if hp_decile == `i'
  }
  qui save `data_`year'', replace
}

* Aggregated data of fuel consumption and horsepower in 1970 and 1990
use `data_70', clear
append using `data_90'
collapse (first) wtd_avg_li hp_min hp_max hp_mid li_hat qu_total, by(ye hp_decile)
label var wtd_avg_li "Sales-weighted average of fuel consumption"
label var hp_mid "Midpoint of horsepower decile"
```

Visualize the relationship between fuel consumption and horsepower.

```{s}
local file_name relation_li_hp
twoway (scatter wtd_avg_li hp_mid if ye == 1970, mcolor(navy%70)) ///
(scatter wtd_avg_li hp_mid if ye == 1990, mcolor(orange%70)) ///
(line li_hat hp_mid if ye == 1970, sort lcolor(navy%70) lpattern(shortdash)) ///
(line li_hat hp_mid if ye == 1990, sort lcolor(orange%70) lpattern(longdash)), ///
xlabel(15(15)150) ylabel(5(2)15) ///
xtitle("Midpoint of each horsepower decile (kW)", size(medsmall)) ///
ytitle("Sales-weighted average of fuel consumption (liter per km)", size(medsmall) margin(top)) ///
title("Relationship between Fuel Consumption and Horsepower in 1970 and 1990", ///
size(medsmall) color(black)) ///
legend(label(1 "1970") label(2 "1990") ///
label(3 "1970 fitted curve") label(4 "1990 fitted curve") ///
nobox region(lcolor(white)) size(small) rows(1)) ///
graphregion(color(white))
graph export "$figure/`file_name'.png", replace
```
\begin{figure}[H]
\centering
\caption{Relationship between fuel consumption and horsepower by year \label{fig:1}}
\includegraphics[width = \textwidth]{/Users/celiazhu/Box/projects/ra_code_sample/io_code_sample/output/figure/relation_li_hp.png}
\end{figure}

Based on figure \ref{fig:1}, in general, the sales-weighted average fuel consumption
increased as the horsepower increased. Comparing with 1970, the sales-weighted
average fuel consumption decreased in 1990 in all horsepower decile groups, and
the fitted curve was flatter in 1990. We can extrapolate that, controlling for
horsepower, the sales-weighted average fuel consumption decreased over time -- cars
in the five European markets became more fuel-efficient.

Suppose a social cost of carbon was imposed across Europe in 1991, causing the price
of gas to increases across all five markets. This imposed social cost of carbon
would disincentivize consumers to buy cars that have a high fuel consumption, or
the cars that have a high horsepower, which positively correlates with fuel consumption.
As a result, the sales-weighted average fuel consumption of the cars in the high
horsepower group would fall in 1991. The fitted curve in 1991 would be flatter than
the one in 1990, especially in the tail of the curve.

Please see table \ref{tab:1} for summary statistics of the sales-weighted average
of fuel consumption by decile of horsepower in 1970 and 1990.

```{s}
* prepare data to generate texsave table
qui gen wtd_avg_li_3 = string(wtd_avg_li,"%4.3f") // round up to 3 decimal places
qui drop wtd_avg_li
rename wtd_avg_li_3 wtd_avg_li
qui reshape wide wtd_avg_li hp_min hp_max hp_mid li_hat qu_total, i(hp_decile) j(ye)
foreach year of numlist 1970 1990 {
  * horsepower range of each decile group
  egen hp_range`year' = concat(hp_min`year' hp_max`year'), punct("--")
  * insert comma in total sales quantity
  gen qu_total`year'_comma = string(qu_total`year', "%15.0fc")
  drop qu_total`year'
  rename qu_total`year'_comma qu_total`year'
}

* reorder variables
order hp_decile wtd_avg_li1970 hp_range1970 qu_total1970 ///
wtd_avg_li1990 hp_range1990 qu_total1990

* drop un-needed variables
drop hp_min* hp_max* hp_mid* li_hat*

* generate summary statistics table
local file_name hp_sum_table
local title title("Sales-weighted average of fuel consumption by decile of horsepower")
local midliners "\cmidrule(lr){2-4} \cmidrule(lr){5-7} \addlinespace[-2.5ex]"
local colnames "{Decile} &{Fuel consumption} &{Horsepower range} &{Sales} &{Fuel consumption} &{Horsepower range} &{Sales} "
local headerlines headerlines("& \multicolumn{3}{c}{1970} & \multicolumn{3}{c}{1990}" "`midliners'" "`colnames'")
local fn footnote("Fuel consumption represents the sales-weighted average of fuel consumption.")
local marker marker("tab:1")
local size size("small")
texsave using "$table/`file_name'.tex", replace ///
loc(H) frag nonames `headerlines' `marker' `fn' `size' `title'
```

\input{"/Users/celiazhu/Box/projects/ra_code_sample/io_code_sample/output/table/hp_sum_table.tex"}



## 3. Estimation and Causal Inference

For each car model $i$, market $j$, and year $t$, construct the outcome variable
$Y_{ijt} = log(S_{ijt}) - log(S_{0ijt})$, where $N_{jt}$ is the number of
consumers in market $j$ year $t$, assuming the average size of family is four and
each family potentially buys one car or no cars in a given year, $S_{ijt}$ is the
market share for car model $i$ in market $j$ year $t$, $S_{0jt}$ is the share of
consumers who buy no cars in market $j$ year $t$.

A standard logit demand model:
\begin{equation*}
U_{cijt} = \beta_c \text{FuelConsumption}_{ijt} + \alpha_c Price_{ijt} + \zeta_{ijt} + \epsilon_{cijt}
\end{equation*}

where $c$ is an index for car consumers, $i$ car model, $j$ market, and $t$ year.
$U$ is the indirect utility, as a function of the car models' average fuel consumption
and price. $\zeta_{ijt}$ is the un-observables on the year-market-model level.
$\epsilon_{cijt}$ is un-observables on the year-market-model-consumer level.

Assume the structural parameters, $\beta$ and $\alpha$, do not depend on individual
consumer (every consumer has the same taste for fuel consumption and price),
$\epsilon_{cijt}$ has type-one extreme value function, and all consumers
who did not buy a car chose the outside option, which gives zero utility. Then,
we can estimate the structural parameter through the following log-linear model:

\begin{equation} \label{eqn:1}
log(S_{ijt}) - log(S_{0ijt}) = \beta \text{FuelConsumption}_{ijt} + \alpha Price_{ijt} + \zeta_{ijt}
\end{equation}

$\zeta_{ijt}$ is a shock on the demand side.

The results of this regression is in model (1) of table \ref{tab:2}. We estimate
the coefficient of fuel consumption, $\beta$, to be $-0.214$. It means that, holding
everything else equal, the consumers' indirect utility decreases by $0.214$ unit
as the fuel consumption of the car increases by $1$ unit.

```{s}
use "$data/clean_data/all_data.dta", clear

* Generate the number of consumers in market j in year t
gen n_consumers = pop/4 // 4 is average family size; assume each family potentially buys one car
label var n_consumers "Number of consumers"

* Generate market share of car model i in market j in year t
gen share = qu/n_consumers
label var share "Market share"

* Generate share of consumers who by no cars in market j in year t
bysort ma_code ye: egen qu_total = total(qu)
gen share_no_cars = 1 - qu_total/n_consumers
label var share_no_cars "Share of consumers who buy no cars"

* Generate outcome variable
gen outcome = ln(share) - ln(share_no_cars)
label var outcome "Outcome"

* Regress outcome variable on a constant, fuel consumption, and price
eststo clear
eststo: qui reg outcome li eurpr, r
qui estadd local fe "None"
```

Price is an endogenous variable in model \ref{eqn:1}. Price is correlated with
$\zeta_{ijt}$, the demand shock. Assuming the demand shock is observable to
manufacturers, then manufacturers would adjust the prices accordingly.

There are two different sources of endogeneity that could bias our estimate of the
structural parameter of price.

\textbf{One source of endogeneity} is the un-observalbe features of the car models, markets, or years.
For example, consumers may prefer one particular type of car models for reasons
other than its fuel consumption or price, but for the models' un-observable features.
Denote them as $\phi_i$. Similarly, consumers may experience
some market-specific, model-and-year-invariant shock, $\phi_j$, or some
year-specifc, model-and-market-invariant shock, $\phi_t$.


We can mitigate this type of endogeneity by including car model, market, and year
fixed effects in model \ref{eqn:1}.

\begin{equation} \label{eqn:2}
log(S_{ijt}) - log(S_{0ijt}) = \beta \text{FuelConsumption}_{ijt} + \alpha Price_{ijt} + \zeta_{ijt} + \phi_i + \phi_j + \phi_t
\end{equation}

We report the coefficients of fuel consumption and price in model (2) of table \ref{tab:2}.

```{s}
* Including car model, market, and year fixed effects
sort ye ma_code model_code
eststo: qui reg outcome li eurpr i.ma_code i.model_code i.ye, r
qui estadd local fe "car model, market, and year"

* Output table for two models
local file_name linear
local title "Structural parameters for fuel consumption and price"
local label \label{tab:2}
sort ye ma_code model_code
local reg_tbl_setting "se booktabs width(\textwidth) label" // settings used for all regression tables
qui esttab using "$table/`file_name'.tex", replace ///
drop(*.ma_code *.model_code *.ye) scalars("fe Fixed effects") ///
title("`title' `label'") nofloat ///
`reg_tbl_setting'
```

\begin{table}[H]
\centering
\input{"/Users/celiazhu/Box/projects/ra_code_sample/io_code_sample/output/table/linear.tex"}
\end{table}

\textbf{Another source of endogenity} comes from the market
structure. For example, if the car markets in the five European countries are not
perfectly competitive, there still exists potential correlation between price
and demand shock even if we controlled for all fixed effects.

We need to use an instrument variable for price. One candidate of the instrument
variable is the transportation cost: the cost needed to transport cars from their
manufacturing locations to their markets. This is because the transportation cost
is the supply-shifter that does not shift the demand: it's uncorrelated with the
demand shock $\zeta_{ijt}$.
