---
title: Stata Code Sample [^1]
author: Xiling (Celia) Zhu <xiling@uchicago.edu>
date: Aug 28, 2020
header-includes:
  - \usepackage{multicol}
  - \usepackage{tabularx}
  - \usepackage{lscape}
  - \usepackage{graphicx}
  - \usepackage{float}
---

## Background

In January 2012, the Cook County State’s Attorney’s Office established a program
intended to reduce re-arrest among people on bail awaiting trial.
The program ran through October 2013.

The objective of our analysis is to evaluate the effectiveness of the program.
We start by cleaning data sets on demographics, arrrests information, and academic performance.
We provide descriptive statistics for the study population
and test their baseline equivalence. The final step is to evaluate whether
participating in the program reduces the likelihood of re-arrest before disposition.

## 0. Preamble
[^1]: Generated by `markstat`. For souce code, please see my github repository [here](https://github.com/CeliaZhu/ra_code_sample/blob/master/stata/ra_stata_sample.stmd).

```{s}
clear all
set more off
set varabbrev off

* Set up directories
global data /Users/celiazhu/Box/projects/ra_code_sample/data
global output /Users/celiazhu/Box/projects/ra_code_sample/stata/output
global processed /Users/celiazhu/Box/projects/ra_code_sample/stata/processed
```

## 1. Data Cleaning

### 1.1 Clean demographic data

```{s}
* Import demographic data
import delimited "$data/demo.csv", clear

* Make sure person_id is uid
duplicates drop
isid person_id
```

The demographic data were extracted from a system that inconsistently coded gender.
Recode it so that males are consistently coded as “M” and females are consistently coded as “F”.

```{s}
tab gender, m
replace gender = "F" if gender == "female"
replace gender = "M" if gender == "male"

* Check if gender is consistently coded
assert gender == "M" | gender == "F"

* Save cleaned demographic data
save "$processed/demo_clean.dta", replace
```


### 1.2 Clean arrests data (data on arrests is named as "case")

Merge the case and demo datasets together so that each row in the case dataset
also contains the demographics of the defendant.
It's possible to have one person with multiple cases.

I didn't find other variables (except person_id) in demo and case sharing same variable names.

```{s}
* Import arrests data
import delimited "$data/case.csv", clear

* Make sure caseid is uid
isid caseid

merge m:1 person_id using "$processed/demo_clean.dta", nogen keep(3)
```

While the program was mostly rolled out to defendants in Chicago,
the State’s Attorney’s Office also ran a pilot serving a small number of individuals
arrested in other parts of Cook County.

For the purpose of this analysis, please restrict the data to only individuals
who were arrested in Chicago.

I first change all addresses to lower case because it's possible to have "Chicago"
inconsistently capitalized as "CHICAGO", "Chicago", or even "chicago".

```{s}
      replace address = lower(address)
      keep if strpos(address, ", chicago") > 0
```

Create an age variable equal to the defendant’s age at the time of arrest for each case.

```{s}
* Glimpse the `arrest_date` and `bdate` to know their formats.
codebook arrest_date bdate
foreach var of varlist arrest_date bdate{
    gen `var'_dt = date(`var', "YMD")
  }
gen age = round((arrest_date_dt - bdate_dt)/365.25,0.1)

* Save cleaned case and demographic data
save "$processed/case_demo_clean.dta", replace
```

### 1.3 Clean grade data for defendants in their early early adulthood

The State’s Attorney’s Office has requested 9th and 10th grade course
grade data from defendants between the ages of 18 and 24.
These data are included in grades.csv.
Construct measures for 9th and 10th grade GPA for this target population.
When constructing GPA, use a 4 point scale, where: A=4, B=3, C=2, D=1, and F=0.

```{s}
* Import grade data
import delimited "$data/grades.csv", clear

* Construct 9th and 10th GPA for defendants between the age of 18 and 24
foreach g of varlist gr* {
  gen n_`g' = cond(`g' == "A", 4, ///
              cond(`g' == "B", 3, ///
              cond(`g' == "C", 2, ///
              cond(`g' == "D", 1, ///
              cond(`g' == "F", 0, .)))))
  }

forvalues i = 9/10{
  local grade`i' n_gr`i'_*
  egen gpa`i' = rmean(`grade`i'')
}

* Sanity check on GPAs
su gpa*

* Keep person id and gpa for grade 9 and 10
keep person_id gpa*
isid person_id

* Save cleaned grades data
save "$processed/grades_clean.dta", replace
```

## 2. Statistical Analysis

Determine if the program should be continued/expanded
by estimating the program’s effect on **re-arrests prior to disposition**.

Because we only have grades data for young adults,
I did not use these data to inform your statistical analysis.

```{s}
use "$processed/case_demo_clean.dta", clear

* The study population should have 25,000 subjects
assert(_N == 25000)

* Create dummies for gender and race.
tab gender, m gen(gender_)
rename gender_1 female
label var female "Female"
rename gender_2 male
label var male "Male"

tab race, m gen(race_)
rename race_1 asian
rename race_2 black
rename race_3 white
label var asian "Asian"
label var black "Black"
label var white "White"

* Label variables for cleaner output.
label var age "Age"
label var prior_arrests "Number of prior arrests"
label var re_arrest "Re-arrested"
label var treat "Enrolled into program"

label define treat_lab 0 "Unenrolled" 1 "Enrolled"
label define male_lab 0"Female" 1"Male"
label define black_lab 0"Non-Black" 1"Black"
label define white_lab 0"Non-White" 1"White"

foreach var of varlist treat male black white {
  label values `var' `var'_lab
}

* Save cleaned data with dummies
save "$processed/analysis_data.dta", replace

* Define a local macro for covariates.
local balancevar "female male asian black white prior_arrests age"
```

### 2.1 Summary statistics of study population

The study population are predominantly male, with only 20% cases having female
defendants. As to race, 73% of the cases involve Black defendants, only 22% are white,
and 5% are Asian. On average, the study population has around 4 prior arrests
before the case arrest date, and their average age is approximately 30.

```{s}
eststo clear
qui estpost su `balancevar'
esttab using "$output/summary_statistics.tex", replace ///
cells("mean(fmt(2)) sd(fmt(0 0 0 0 0 0 1)) min(fmt(0 0 0 0 0 0 1)) max(fmt(0 0 0 0 0 0 1))") ///
collabel("Mean" "Standard Deviation" "Min" "Max" ) ///
width(\textwidth) nonumber  label
```
\begin{table}[H]
\centering
\caption{Summary statistics of study population}
\input{/Users/celiazhu/Box/projects/ra_code_sample/stata/output/summary_statistics.tex}
\end{table}

### 2.2 Balance tests for demographic characteristics

The enrolled and unenrolled groups are not balanced at the baseline.

The average numbers of prior arrests are significantly different in the two groups.
Cases with more prior arrests are more likely to be enrolled into the program.

Their age is also imbalanced. Cases with older defendants are more likely to be enrolled
into the program.

The imbalance are not due to random coincidence.
The F-test indicates that these covariates didn't pass joint orthogonality, either.

The imbalance at baseline signals the problem of selection.

```{s}
iebaltab `balancevar', grpvar(treat) ///
vce(robust) savetex("$output/balance_test.tex") replace ///
rowvarlabels pttest ftest fnoobs pftest
```
\begin{table}[H]
\centering
\caption{Balance test}
\input{/Users/celiazhu/Box/projects/ra_code_sample/stata/output/balance_test.tex}
\end{table}

### 2.3 Visualize number of prior arrests by enrollment status and race

Create a numerical variable for race

```{s}
preserve
encode race, gen(n_race)
qui: codebook n_race

gen avg = .
gen ci_low = .
gen ci_high = .

* Calculate means and confidence intervals
qui: mean prior_arrests, over(treat n_race)
matrix M = r(table)
matrix list M

forvalues i = 1/6 {
  if inrange(`i', 1, 3) == 1{
    replace avg = M[1, `i'] if treat == 0 & n_race == `i'
    replace ci_low = M[5, `i'] if treat == 0 & n_race == `i'
    replace ci_high = M[6, `i'] if treat == 0 & n_race == `i'
  }

  if inrange(`i', 4, 6) == 1 {
  	replace avg = M[1, `i'] if treat == 1 & n_race == `i'-3
    replace ci_low = M[5, `i'] if treat == 1 & n_race == `i'-3
    replace ci_high = M[6, `i'] if treat == 1 & n_race == `i'-3
  }
}

* Count observations
forvalues i = 1/6 {
  if inrange(`i', 1, 3) == 1 {
    count if treat == 0 & n_race == `i' & !missing(prior_arrests)
  }

  if inrange(`i', 4, 6) == 1 {
  	count if treat == 1 & n_race == `i'-3 & !missing(prior_arrests)
   }

   local `i'N = r(N)
 }

* Plot the bar chart
gen treat_race = n_race if treat == 0
replace treat_race = n_race + 4 if treat == 1
twoway (bar avg treat_race if n_race == 1, fcolor(maroon) ///
          fintensity(inten70) lcolor(white) barw(0.7)) ///
        (bar avg treat_race if n_race == 2, fcolor(maroon) ///
          fintensity(inten50) lcolor(white) barw(0.7)) ///
        (bar avg treat_race if n_race == 3, fcolor(maroon) ///
          fintensity(inten30) lcolor(white) barw(0.7)) ///
  	    (rcap ci_low ci_high treat_race, lcolor(gs5)), ///
        legend(row(1) order(1 "Asian" 2 "Black" 3 "White" 4 "95% C.I.")) ///
        xlabel(2"Unenrolled" 6"Enrolled", noticks) xtitle("Enrollment status") ///
        ylabel(0(1)4) ytitle("Average number of prior arrests", ///
        margin(medium) size(medium)) ///
        title("Number of prior arrests imbalanced" ///
        "between enrolled and unenrolled groups", size(medium)) ///
        note("Number of defendants:" ///
        "Asian unenrolled `1N', Black unenrolled `2N', White unenrolled `3N'" ///
      	"Asian enrolled `4N', Black enrolled `5N', White enrolled `6N'") ///
        caption("Source: Cook County State's Attorney's Office", ///
        justification(left) size(vsmall) linegap(0.8) position(5) span) ///
        graphregion( color(white) ) plotregion(fcolor(white))

graph export "$output/prior_arrests.png", replace
restore
```
\begin{figure}[H]
\centering
\caption{Number of prior arrests imbalanced bewteen enrolled and unerolled groups}
\includegraphics[width = \textwidth]{/Users/celiazhu/Box/projects/ra_code_sample/stata/output/prior_arrests.png}
\end{figure}

### 2.4 Estimate the effect of the program on reducing the likelihood of re-arrest before disposition

One difficulty in estimating the effect of the program is that I don't have
enough information about the program: if program was an randomized controlled trial
and if so, how was the compliance, or if it was an observational study.

#### Specification 1: OLS (or Linear Probaility Model)

We start with ``naive" OLS model.

$$
		rearrest_{ic} = \tau treat_{ic} + \beta X_{ic} + \epsilon_{ic} + \epsilon_{i}
$$

where $i$ is the individual,  $c$ is the case, $X_{ic}$ is the vector for race,
gender, age, and number of prior arrests.
We could have added individual fixed effects $u_{i}$ to control for those
individual-invariant characteristics. But by examining the unique values of
`person_id`, we can conclude that for most defendants, they only have one or two
cases. Individual fixed effect is not desirable here.

It can correctly estimate the average treatment effect (ATE) if
this program was a \textbf{randomized experiment} with perfect compliance;
or it was an \textbf{observational study} satisfying the two conditions:
1) there is no selection on unobservables and we've controlled
all observables that could be selected upon;
and 2) how people are self-selected based on those variables can be approximated
by a linear function. But these conditions are unlikely to be true.

That said, suppose the program was a randomized controlled trial, the treatment was
administered on the case level, not on the individual level.
Hence, we don't cluster standard errors here.

#### Specification 2: Logit

Assume the program was not an experimental study, we can improve our \textbf{prediction} on the likelihood
by using a logit model instead of a linear probability model, which was implemented in
specification 1. But the results given by logit specification is for prediction,
not for causal inference.

$$
  Pr(rearrest = 1 | X_c) = \frac{exp^{X_c'\beta}}{1 + e^{X_c'\beta}}
$$


#### Specification 3: Inverse Probability Weighting (IPW)

To estimate the causal effect via propensity score matching (more specifically,
inverse probability weighting (IPW)), given that the program was \textbf{observational},
not experimental, we still need to assume that there is \textbf{no selection on unobservables}
and we've controlled all observables that could be selected upon.
One improvement from OLS or logit model is that we can relax the assumption on the
functional form.

One improvement from OLS or logit model is that we can relax the assumption on
the functional form. Still, the estimate based upon IPW is not entirely valid,
because we omit important characteristics such as grades, household income,
neighborhood, etc.. However, this is less restrictive and therefore more plausible
than the other two models.

First, calculate propensity and inverse probability weight (ipw).

```{s}
// Get propensity score
* Let female and asian be base groups
local covlist "male black white prior_arrests age"
qui logit treat `covlist'
predict pscore, pr

// Inverse probability weighting (IPW)
* Weight for average treatment effect (ATE)
gen double ate_weight = 1.treat/pscore + 0.treat/(1-pscore)
* Weight for average treatment effect on the treated (ATET)
gen double atet_weight = 1.treat/1 + 0.treat * pscore/(1-pscore)

// Common support
gen support = 1
forvalues i = 1/2 {
        su pscore if treat == `i' -1
        replace support = 0 if inrange(pscore, r(min), r(max)) == 0
      }
```

Before we estimate average treatment effect (ATE) and average treatment effect
on the treated (ATET) of this program, we first assess the validity of propensity
score by 1) the distribution of the propensity score, 2) summary statistics of
observations within the common support, and 3) the balance of the sample within
common support, weighted by the IPW for ATE.

1) Assess the distribution of the propensity score in enrolled and unerolled groups

```{s}
su pscore if support == 1
local lhs = r(min)
local rhs = r(max)
di `lhs'
di `rhs'
histogram pscore, xline(`lhs', lcolor(black) lwidth(thin) lpattern(dash)) ///
xline(`rhs', lcolor(black) lwidth(thin) lpattern(dash)) ///
freq width(0.05) ///
by(treat, row(2) graphregion(color(white)) note("Source: Cook County State's Attorney's Office" ///
"(Observations bounded by vertical lines within common support)", size(small))) ///
fcolor(maroon%70) lcolor(white%0) ///
xtitle("Propensity score", size(medsmall)) ///
ytitle("Frequency", size(medsmall))

graph export "$output/ps_hist.png", replace
```

\begin{figure}[H]
\centering
\caption{histogram of propensity score by enrollment status}
\includegraphics[width = \textwidth]{/Users/celiazhu/Box/projects/ra_code_sample/stata/output/ps_hist.png}
\end{figure}

2) Summary statistics of observations within common support
```{s}
eststo clear
qui estpost su `balancevar' if support == 1
esttab using "$output/summary_statistics_cs.tex", replace ///
cells("count(fmt(0)) mean(fmt(2)) sd(fmt(2)) min(fmt(0 0 0 0 0 0 1)) max(fmt(0 0 0 0 0 0 1))") ///
collabel("N" "Mean" "Standard Deviation" "Min" "Max" ) ///
width(\textwidth) nonumber  label
```
\begin{table}[H]
\centering
\caption{Summary statistics for observations within common support}
\input{/Users/celiazhu/Box/projects/ra_code_sample/stata/output/summary_statistics_cs.tex}
\end{table}

3) Balance test for observations within common support, weighted by inverse probability
for ATE

```{s}
iebaltab `balancevar'  if support == 1 [pw = ate_weight], grpvar(treat)  ///
vce(robust) savetex("$output/balance_test_ipw.tex") replace ///
rowvarlabels pttest ftest fnoobs pftest
```
\begin{table}[H]
\centering
\caption{Balance test for observation within common support, weighted by inverse probability for ATE}
\input{/Users/celiazhu/Box/projects/ra_code_sample/stata/output/balance_test_ipw.tex}
\end{table}

OLS regression
```{s}
unique person_id
eststo clear
eststo: qui reg re_arrest treat `covlist', rob
```

Logit regression
```{s}
eststo: logit re_arrest treat `covlist', rob cluster(person_id)
```

Estimate ATE and ATET with inverse probability weighting.
```{s}
eststo: qui reg re_arrest treat [pw = ate_weight] if support == 1
eststo: qui reg re_arrest treat [pw = atet_weight] if support == 1
```

Export results from three specifications
```{s}
esttab using "$output/estimation.tex", se  label nobaselevels noomitted ///
addnotes("Model 1: OLS; Model 2: Logit; Model 3: IPW (ATE); Model 4: IPW (ATET)" ///
"Odds ratio reported in logit model" ///
"Robust s.e. reported in OLS model; clustered robust s.e. reported in logit model") ///
eform(0 1 0 0 ) replace
```

\begin{table}[H]
\centering
\caption{Estimation}
\input{/Users/celiazhu/Box/projects/ra_code_sample/stata/output/estimation.tex}
\end{table}

## 3. Conclusion
Overall, the treatment significantly reduces the likelihood of re-arrest before disposition.
With the information we have from the pilot study, we can conclude that the program is
effective and should be furthered examined with an experiment.

However, please note that the causal inference has much room for improvement. It
would have better performance if we can obtain more characteristics. To name a few,
household income, and the neighborhoods defendants live in; and for younger defendants,
we can also incorporate their academic performance and disciplinary incidents in school,
and the school districts they live in.
